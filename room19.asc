// room script file

bool hamishTurned;

function hamishLoseMug()
{
  while(cHamish.HasInventory(iMug))
  {
    cHamish.LoseInventory(iMug);
  }
}

function room_Load()
{
  
  hamishTurned = false; 
  
 
  resumeMusicAfterCHCH(aEndlessOffice);
  changePlayerLocaleOnRoomEnter();
  player.PlaceOnWalkableArea();
  
  if(characterChange)
  {
    Employees_Omni.putCharsInPlace();
    characterChange = false;    
  }
  
  if(cHamish.Room == eRooms_Omni_IT_Office)
  {
    RemoveWalkableArea(2);
    hamishLoseMug();
    oComputerScreen.Graphic = 1693;
    hamishFacingScreen = true;
    cHamish.SetProperty("normalSpeechView", 68);
    cHamish.SetProperty("awkwardView", 68);  
    cHamish.SetProperty("angryView",68);
    cHamish.ChangeView(68);   
    cHamish.cool();
    if(!cHamish.isAt(176, 145))
    {
      cHamish.place(176, 145);     
      cHamish.Loop = 0;
    }
  } else {
    RestoreWalkableArea(2);
  }
}

function room_AfterFadeIn()
{
  //add cutscene here first time
 // Game.Camera.TweenX(0.2, 0, eEaseInOutSineTween, eNoBlockTween);
  if(cHamish.Room == eRooms_Omni_IT_Office)
  {
    SetTimerWithSeconds(12, 0.5);
  }
}

function region1_WalksOnto()
{
  player.ChangeRoom(eRooms_Omni_CorridorRight, player.x + 478, player.y);
}

function room_FirstLoad()
{
  //cHamish.Baseline = cHamish.y - 8;
  
}


//not working try something else
//trying to get Hamish to follow abi with his eyes
function hamishEyes(bool ontoRegion)
{
 
  if(cHamish.Room == eRooms_Omni_IT_Office && hamishTurned && !cHamish.Speaking && !cHamish.Animating)
  {
    if(ontoRegion)
    { 
      if(player.x > 173)
      {
        hamishView = 75;
        hamishTalkView = 76;
        cHamish.SetProperty("normalSpeechView",76);
        cHamish.ChangeView(75);
        cHamish.SpeechView = 76;
      } else {
        hamishView = 71;
        hamishTalkView = 73;
        cHamish.ChangeView(71);
        cHamish.SpeechView = 73;
        cHamish.SetProperty("normalSpeechView",73);
      }
    } else {
      if(player.x < 173)
      {
        hamishView = 75;
        hamishTalkView = 76;
        cHamish.SetProperty("normalSpeechView",76);
        cHamish.ChangeView(75);
        cHamish.SpeechView = 76;
      } else {
        hamishView = 71;
        hamishTalkView = 73;
        cHamish.ChangeView(71);
        cHamish.SpeechView = 73;
        cHamish.SetProperty("normalSpeechView",73);
      }
    }
    cHamish.cool();
  }
}

function stopGoComputer()
{
  
  if(IsTimerExpired(12))
  {
    oComputerScreen.Graphic = 1694;
    oComputerScreen.SetView(74, 0, 0);
    oComputerScreen.Animate(0, 1, eRepeat, eNoBlock, eForwards, 0);
    
  }
  if(!hamishTurned)
  {
    if(!hamishFacingScreen)
    {
      hamishTurned = true;
      oComputerScreen.StopAnimating();
    }
  } else {
    if(hamishFacingScreen)
    {
      hamishTurned = false;
      oComputerScreen.Animate(0, 1, eRepeat, eNoBlock, eForwards, 0);
    }
  }
}

function late_repeatedly_execute_always()
{
  stopGoComputer();
}

function room_RepExec()
{
  
  
}

function oKeyCard_AnyClick()
{

switch (verbUsed)
{
  case 0: //walk
    walkToAndFace(1, 1, eDirectionLeft, 2, 2); 
    //or
    player.Walk(1, 1);
  break;
  case 1://look
    player.FaceLocation(1, 1, eBlock);
    lookAbiPaul("It's the key-card for the stationery closet!", "It's a key-card.");
  break;
  case 2://use
    Canned.noUsing();
  break;
  case 3://talk
    Canned.noTalking();
  break;
  case 4://move
    Canned.noMoving();
  break;
  case 5://fourletters
    Canned.fuckAnyone();
  break;
  case 6://take
    oKeyCard.Visible = false;
    player.AddInventory(iKeyCard);
    displayAlert(eAlertInvPickup, false, iKeyCard);
  break;
  case 7://inv
    Canned.wontWork();
  break;
}
verbUsed = 0;

}

function region2_WalksOnto()
{
  hamishEyes(true);
}

function region2_WalksOff()
{
  hamishEyes(false);
}

function hExit_AnyClick()
{

switch (verbUsed)
{
  case 0: //walk

    player.Walk(144, 172);
  break;
  case 1://look
    player.FaceLocation(144, 172,  eBlock);
    lookAbiPaul("That's the way out.", 
                "The exit to the corridor.");
  break;
  case 2://use
    Canned.noUsing();
  break;
  case 3://talk
    lookAbiPaul("Don't come in!",
                "Stay out, I'm in here!");
  break;
  case 4://move
    Canned.noMoving();
  break;
  case 5://fourletters
    Canned.fuckAnyone();
  break;
  case 6://take
    Canned.cantTake();
  break;
  case 7://inv
    Canned.wontWork();
  break;
}
verbUsed = 0;
}

function interactComputer()
{
  
  switch (verbUsed)
  {
    case 0: //walk
      if(cHamish.Room == eRooms_Omni_IT_Office)
      {
      walkToAndFace(191, 140, eDirectionLeft, 201, 145);   
      } else {    
      walkToAndFace(175, 140, eDirectionUpRight, 158, 135); 
      }
    break;
    case 1://look
      player.FaceLocation(186, 126, eBlock);
      lookAbiPaul("Hamish's computer.", "That computer guy's computer.");
    break;
    case 2://use
      if(cHamish.Room == eRooms_Omni_IT_Office)
      {
        cHamish.Say("Don't touch that!");
      } else {
        getOutOfTheWay(175, 140);
        player.FaceDirection(eDirectionUpRight);
        //animate jabbing the keys
        player.Say("beep boop beep boop");
        WaitSeconds(0.4);
        aComputerError.Play();
        oComputerScreen.Graphic = 1695;
      }
      
    break;
    case 3://talk
      Canned.noTalking();
    break;
    case 4://move
      Canned.noMoving();
    break;
    case 5://fourletters
      Canned.fuckAnyone();
    break;
    case 6://take
      Canned.cantTake();
    break;
    case 7://inv
      Canned.wontWork();
    break;
  }
  verbUsed = 0;

}

function oComputerScreen_AnyClick(){  interactComputer(); }
function hKeyboard_AnyClick(){        interactComputer(); }
