// room script file

function setViewNum(Object* theObject, int num)
{
  theObject.SetView(27, 0, num);
}



int theHour;
int theMinute;
int alarmHour;
int alarmMinute;


function setAlarmDefault()
{
  theHour = Time.GetHour();
  theMinute = Time.GetMinute();
  alarmHour = theHour;
  alarmMinute = theMinute;
}

//function which splits ones and tens for each of these values. 
//can do with maths or string functions. surely maths would be easier/faster??

function splitNum(int theNum, bool trueOnes)
{
  int ones;
  int tens = 0;
  String numAsString = String.Format("%d", theNum);
 
  if(theNum < 10)
  {
    ones = theNum;
  } else {
    String Stens = numAsString.Substring(0, 1);
    String Sones = numAsString.Substring(1, 1);
    
    tens = Stens.AsInt;
    ones = Sones.AsInt;
  }
      
  if(trueOnes)
  {
    return ones;
  } else {
    return tens;
  }
}

function setViewsNums()
{
  int displayHour = alarmHour;

  if(alarmHour > 12)
  {
    displayHour -= 12;    
  } 
  if(alarmHour == 0)
  {
    displayHour = 12;
  }
  
  if(alarmHour >= 0 && alarmHour < 12)
  {
    oPm.Visible = false;
  } else {
    oPm.Visible = true;
  }
  
  setViewNum(oMinOnes, splitNum(alarmMinute, true));  
  setViewNum(oMinTens, splitNum(alarmMinute, false));
  setViewNum(oHourOnes, splitNum(displayHour, true));
  setViewNum(oHourTens, splitNum(displayHour, false));
 if(splitNum(displayHour, false) == 0)
  {
    oHourTens.Visible = false;
  } else {
    oHourTens.Visible = true;
  }
  
  
}

function room_Load()
{
  Time.FreezeTime();
  setAlarmDefault();
  setViewsNums();
 
}

function plus()
{
  if(alarmMinute < 59)
  {
    alarmMinute++;
  } else {
    alarmMinute = 0;
    if(alarmHour < 23)
    {
      alarmHour++;
    } else {
      alarmHour = 0;
    }
  }
  setViewsNums();
  
  player.SayBackground(String.Format("%d:%d",alarmHour, alarmMinute));
  
}

function room_RepExec()
{
  
  if(Object.GetAtScreenXY(mouse.x, mouse.y) == oSetButton && mouse.IsButtonDown(eMouseLeft))
  {
    plus();
  }
}

function room_Leave()
{
  Time.FreezeTime(false);
}
