// room script file
int currentPhoneme = 0;
function raiseBrows()
{
  if(currentPhoneme == 1)
  {
    oJohnBrows.Animate(0, 2, eOnce, eNoBlock, eForwards);
    currentPhoneme = 0;
    
  }
}

function lowerBrows()
{
  if(currentPhoneme == 2)
  {
     oJohnBrows.Animate(0, 5, eOnce, eNoBlock, eBackwards);
     currentPhoneme = 0;
  }
}

function worry()
{
  if(currentPhoneme == 3)
  {
    oWorry.Animate(0, 0, eOnce, eNoBlock);
    currentPhoneme = 0;
  }
}



function lookDownJohn()
{
  oLookDown.SetView(8, 0, 0);
  oLookDown.Animate(0, 0, eOnce, eNoBlock, eForwards, 0);
}

function quickLook()
{
  oLookDown.SetView(9, 0, 0);
  oLookDown.Animate(0, 0, eOnce, eNoBlock, eForwards, 0);
}
function JMSay(String johnSays)
{
  cJohnsMouth.SayAtSync(260, 60, 100, johnSays);
  //cJohnsMouth.SayAt(260, 60, 100, johnSays);
 // cJohnsMouth.SaySync(johnSays);
}

function johnGetAngry()
{
  if(currentPhoneme == 5)
  {
  oAngryBrows.SetView(206, 0, 0);
  oAngryBrows.Visible = true;
  oAngryBrows.Animate(0, 3, eOnce, eNoBlock);
  }
}

function johnChill()
{
  if(currentPhoneme == 6)
  {
  oAngryBrows.Animate(0, 2, eOnce, eNoBlock, eBackwards);
  }
}



function room_AfterFadeIn()
{
  switch(cutSceneShot)
  {
    case 0:
    Game.SetAudioTypeSpeechVolumeDrop(eAudioTypeMusic, 0);
    //Speech.SkipStyle = eSkipTime;
    StartCutscene(eSkipESCOrRightButton);
    JMSay("&1 Problem, Abigeil.");       
    break;
    case 2:
      JMSay("&2 Sorry,");
      WaitSeconds(0.2);
      JMSay("&3 i realise i made that sound like a question,");  
      JMSay("&4 but it was actually meant to be a statement.");

    break;
    case 4:
      JMSay("&5 Problem, Abigeil.");
      WaitSeconds(0.2);
      JMSay("&6 Problem.");
    
    break;
    case 6:
      WaitSeconds(0.2);
      lookDownJohn();
      JMSay("&7 Hmm.");
      WaitSeconds(0.2);
      JMSay("&8 I'm afraid there's something we need to talk about.");
     // JMSay("&9 Um, there's been a few complaints about you.");
    break;
    case 8:
      //JMSay("&10 Look.");
      //JMSay("&11 Whoever it was,");
  
      JMSay("&12 they wanted to remain anonymous, and I respect that.");
    break;
    case 10:
      WaitSeconds(0.8);
      quickLook();
      JMSay("&13 It's uh..");
      JMSay("&14 Mmm."); 
      WaitSeconds(0.2);
      JMSay("&15 Now look,");
      JMSay("&16 there's been some comments about your...");
      lookDownJohn();
      WaitSeconds(0.4);
      JMSay("&17 Your lack of professionalism,");

    break;
    case 12:
      JMSay("&18 Numerous dress-code violations,");
    break;
    case 14:
      quickLook();
      WaitSeconds(0.2);
      JMSay("&19 A constant stream of sarcastic remarks,");
      quickLook();
      JMSay("&20 described here as:");
      
      JMSay("&21 'mildly amusing at best'.");
    break;
    case 16:
    WaitSeconds(2.0);  
    quickLook();
    WaitSeconds(0.3);
    JMSay("&22 We've got a situation, now,"); 
    JMSay("&34 and,"); 
    JMSay("&23 we've had to consult the prophecy.");
    break;
    case 18:
      JMSay("&24 It's actually had a bit to say about you.");
    break;
    case 20:
    JMSay("&25 It's confirmed what a lot of us were actually already thinking,");
    quickLook();
    JMSay("&26 Ah...");
    JMSay("&27 Your employment here is no longer tenable.");  
    break;
    case 22:
      JMSay("&28 Box of b--!");
      JMSay("&29 The prophecy is a sophisticated forecasting instrument");
      JMSay("&30 which may, or may not contain birds.");
      quickLook();
      WaitSeconds(0.1);
     
      JMSay("&31 That's not really any of your business.");
      JMSay("&32 The prophecy actually said that you would terminate your own employment.");
    break;
    case 24:
      lookDownJohn();
      JMSay("&33 Incredible, just as the prophecy foretold...");

    break;
  }
  
    cutSceneShot++;
    player.ChangeRoom(305);
}

function room_Load()
{
 // Speech.VoiceMode = eSpeechVoiceOnly;
 // SetGameOption(OPT_LIPSYNCTEXT, 1);
  oJohnBrows.SetView(10, 0, 0);
  oLookDown.SetView(8, 0, 0);
  cJohnsMouth.Baseline = 4;
  oJaw.Y = -4000;
  oWorry.SetView(11, 0, 0);
  gIconbar.Visible = false;
  //game.lipsync_speed = 12;
 // game.text_speed = 12;
  
}

function room_Leave()
{
//  SetGameOption(OPT_LIPSYNCTEXT, 0);
}

function moveJaw()
{
 
 
  if(
    cJohnsMouth.Frame == 6 ||
    cJohnsMouth.Frame == 7 ||
    cJohnsMouth.Frame == 8 ||
    cJohnsMouth.Frame == 9 
  )
  {    
    
    oJaw.Y = 204;
    
  } else if(cJohnsMouth.Frame == 11)
  {
      
      oJaw.Y = 206;
  } else {
    oJaw.Y = -4000;
 
  }
 
}
int counter = 0;

int dryEyes = 70;
function jonBlink()
{
  counter++;
  if(counter == dryEyes)
  {
    oBlink.Visible = true;
    
  } else if (counter == dryEyes + 8)
  {
    oBlink.Visible = false;
    dryEyes = 70 + Random(100);
    counter = 0;
  }
}

function getPhonemeStressNum()
{ 
  if(TotalLipSync.GetCurrentPhoneme()!= null)
  {
    String theString = TotalLipSync.GetCurrentPhoneme();
    if(theString.EndsWith("1"))
    {
      currentPhoneme = 1;
    } else if (theString.EndsWith("2"))
    {
      currentPhoneme = 2;
    } else if(theString.EndsWith("3"))
    {
      currentPhoneme = 3;
    } else if(theString.EndsWith("5"))
    {
      currentPhoneme = 5;
    } else if (theString.EndsWith("6"))
    {
      currentPhoneme = 6;
    } else
    {
      currentPhoneme = 0;
    }
  } else {
    currentPhoneme = 0;
  }
}


function late_repeatedly_execute_always()
{
  getPhonemeStressNum();
  moveJaw();
  jonBlink();
  raiseBrows();
  lowerBrows();
  worry();
  johnGetAngry();
  johnChill();
}

function room_RepExec()
{
  
}
